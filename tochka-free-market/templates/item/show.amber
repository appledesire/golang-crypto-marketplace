extends ./../layout

block left_menu
  +leftMenu("sellers")

block content
  $l = Localization
  $viewStore = ViewStore
  $viewUser = ViewUser
  $item = ViewItem
  div.ui.grid.divided.top-aligned
    div.three.wide.column.t-left-menu-items
      import ./../store/mixin_store_card
        div.user.info
          +storeCard($viewStore, $l)
    div.thirteen.wide.column
      import ./../store/mixin_store_menu
      +storeMenu($viewStore, $l, "items")
      div.ui.segment.inverted.t-item-image.black[style="text-align: left"]
        h2.ui.dividing.header 
          if $item.Score > 0.0
            div.ui.label.right.floated.large
              i.icon.thumbs.up
              span #{$item.Score}
          | #{$item.Name}
        img.ui.rounded.image[src="/item-image/" + $item.Uuid]
      div.ui.segment
        h3.ui.dividing.header #{Localization.Items.About}
        div #{$item.DescriptionHTML}
      div
        div.ui.segment
          h3.ui.dividing.header #{Localization.Items.Purchase}
          if len(GroupPackagesByTypeOriginDestination) > 0
            each $key, $groupPackages in GroupPackagesByTypeOriginDestination
              table.ui.celled.table.fluid.inverted.green
                thead
                  tr
                    th[style="width:50%"]
                      | #{$l.Items.Type}:
                      br
                      span.ui.label
                        if $key.Type == "mail"
                          #{$l.Items.Mail}
                        if $key.Type == "digital"
                          #{$l.Items.Digital}
                        if $key.Type == "drop"
                          #{$l.Items.Drop}
                        if $key.Type == "drop preorder"
                          #{$l.Items.DropPreorder}
                    if $key.Type == "mail"
                      th
                        | #{$l.Items.ShippingFrom}:
                        br
                        span.ui.label #{$key.CountryNameEnShippingFrom}
                    if $key.Type == "mail" || $key.Type == "drop" || $key.Type == "drop preorder"
                      th
                        | #{$l.Items.ShippingTo}:
                        br
                        span.ui.label #{$key.CountryNameEnShippingTo}
                    if $key.Type == "drop" || $key.Type == "drop preorder" 
                      th
                        | #{$l.Items.City}:
                        br
                        span.ui.label #{$key.City}
                      if $key.CityMetroStation != ""
                        th
                          | Area:
                          br
                          span.ui.label #{$key.CityMetroStation}                        
              table.ui.very.basic.table
                thead
                  th[style="width:50%"] #{$l.Items.PackageName}
                  th #{$l.Items.Price}
                tbody
                  each $group in $groupPackages
                    tr
                      td #{$group.PackageName}
                      td
                        a.ui.button.dark-green.fluid[href="/store/"+$viewStore.Storename+"/item/"+$item.Uuid+"/package/" + $group.Hash +"/book"]
                          if $viewUser.Currency == "USD"
                            #{$group.PriceUSDStr} USD
                          if $viewUser.Currency == "BTC"
                            #{$group.PriceBTCStr} BTC
                          if $viewUser.Currency == "RUB"
                            #{$group.PriceRUBStr} RUB
                          if $viewUser.Currency == "AUD"
                            #{$group.PriceAUDStr} AUD
                          if $viewUser.Currency == "GBP"
                            #{$group.PriceGBPStr} GBP
                          if $viewUser.Currency == "EUR"
                            #{$group.PriceEURStr} EUR
                      
          else
            div.header #{Localization.Items.OutOfStock}
      if len($item.ViewRatingReviews) > 0
        div.ui.segment
          h3.ui.dividing.header #{Localization.Items.Reviews}
          div.ui.comments
            each $review in $item.ViewRatingReviews
              div.comment
                span.ui.label.tiny.right.floated.dark-green
                  i.icon.thumbs.up
                  span #{$review.ItemScore}
                if $review.TransactionAmount == "SMALL"
                  span.ui.label.tiny.right.floated.red SMALL TRANSACTION
                a.avatar[href="/user/"+$review.User.Username]
                  img[src="/user-avatar/" + $review.User.Username + "?size=small"]
                div.content
                  a.author[href="/user/"+$review.User.Username] @#{$review.User.Username}
                  div.metadata
                    span.date #{$review.CreatedAtStr}
                  div.text
                    pre.text #{$review.ItemReview}
      if CanEdit
        div.ui.segment.inverted
          form.ui.form[method="POST"][action="/store-admin/"+$viewStore.Storename+"/item/"+$item.Uuid+"/delete"]
            button.ui.button.red.right.floated[type="submit"]
              i.edit.remove.icon 
              | #{Localization.Items.DeleteItem}
            a.ui.button.teal.right.floated[href="/store-admin/"+$viewStore.Storename+"/item/"+$item.Uuid+"/edit"]
              i.icon.edit
              | #{Localization.Items.EditItem}
          $localization = Localization
          h3.ui.header #{Localization.Items.EditPackages}
          table.ui.table
            thead
              tr
                th #{$l.Items.PackageName}
                th #{$l.Items.Type}
                th #{$l.Items.ShippingTo}
                th #{$l.Items.City}
                th #{$l.Items.ShippingFrom}
                th #{$l.Items.Price}
            tbody
              $item = ViewItem
              $localization = Localization
              each $a in ViewPackages
                tr
                  td #{$a.Name}
                  td
                    span.ui.label.tiny
                      if $a.Type == "mail"
                        #{$localization.Items.Mail}
                      if $a.Type == "digital"
                        #{$localization.Items.Digital}
                      if $a.Type == "drop"
                        #{$localization.Items.Drop}
                      if $a.Type == "drop preorder"
                        #{$localization.Items.DropPreorder}
                  td
                    i.icon.globe
                    | #{$a.CountryNameEnShippingTo}
                  td
                    i.icon.globe
                    | #{$a.GeoCity.NameEn}
                  td
                    i.icon.globe
                    | #{$a.CountryNameEnShippingFrom}
                  td
                    i.icon.dollar 
                    | #{$a.PriceUSD}
                tr
                  td[colspan="6"]
                    form.ui.left.floated[method="POST"][action="/store-admin/"+$viewStore.Storename + "/item/"+$item.Uuid+"/package/"+$a.Uuid+"/delete"]
                      button.ui.button.tiny.basic.red[type="submit"]
                        i.remove.icon
                        | #{$localization.Items.Delete}
                      a.ui.button.tiny.basic.teal[href="/store-admin/"+$viewStore.Storename + "/item/" + $item.Uuid+"/package/"+$a.Uuid+"/edit"] 
                        i.edit.icon
                        | #{$localization.Items.Edit}
                    form.ui.form.left.floated.inline[method="POST"][action="/store-admin/"+$viewStore.Storename+"/item/"+$item.Uuid+"/package/"+$a.Uuid+"/duplicate"]
                      button.ui.button.tiny.basic.teal[type="submit"]
                        i.plus.icon
                        |  #{$localization.Items.Duplicate}
          div.ui.section.divider
          a.ui.button.teal[href="/store-admin/"+$viewStore.Storename+"/item/"+$item.Uuid+"/package/new/edit"] 
            i.add.sign.icon
            |  #{Localization.Items.AddPackage}